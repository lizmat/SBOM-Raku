use SBOM::Raku:ver<0.0.4>:auth<zef:lizmat> <source-sbom>;

my constant $filename = 'SOURCE.cdx.json';

my str @errors;
my sub add-error(IO::Path:D $io) {
    @errors.push: $io.absolute;
}

my int $created;
my int $updated;
my sub produce-sbom(IO() $io) {
    with (try source-sbom($io)) -> $sbom {
        my $dir := $io.sibling(".META");
        $dir.mkdir;
        my $sbom-io := $dir.add($filename);
        my $existed := $sbom-io.s;

        $sbom-io.spurt($sbom.JSON);
        if $sbom-io.s {
            if $existed {
                ++$updated;
            }
            else {
                indir $dir, {
                    my $proc := run <git add>, $filename, :out, :err;
                    note $proc.err.slurp.chomp if $proc.exitcode;
                }
                ++$created;
            }
        }
        else {
            add-error($io);
        }
    }
    else {
        add-error($io);
    }
}

#| Produce source SBOMs in their associated .META directory for the
#| given META6.json files
sub MAIN(*@jsons) {
    if @jsons {
        produce-sbom($_) for @jsons;

        if @errors {
            note "Files that did not produce a source SBOM:";
            note "  $_" for @errors;
        }
        if $created {
            note "Created $created source SBOM{'s' if $created > 1}";
        }
        if $updated {
            note "Updated $updated source SBOM{'s' if $updated > 1}";
        }
    }
    else {
        note "No META6.json files specified";
    }
}

# vim: expandtab shiftwidth=4
